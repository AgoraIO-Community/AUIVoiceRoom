plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "io.agora.voicechat.uikit"
        minSdk 24
        targetSdk 32
        versionCode 2
        versionName "0.2.0"

        def properties = new Properties()
        properties.load(rootProject.file("local.properties").newDataInputStream())
        /**
         * Agora APP ID.
         * Agora assigns App IDs to app developers to identify projects and organizations.
         * If you have multiple completely separate apps in your organization, for example built by different teams,
         * you should use different App IDs.
         * If applications need to communicate with each other, they should use the same App ID.
         * In order to get the APP ID, you can open the agora console (https://console.agora.io/) to create a project,
         * then the APP ID can be found in the project detail page.
         *
         * 声网APP ID
         * Agora 给应用程序开发人员分配 App ID，以识别项目和组织。如果组织中有多个完全分开的应用程序，例如由不同的团队构建，
         * 则应使用不同的 App ID。如果应用程序需要相互通信，则应使用同一个App ID。
         * 进入声网控制台(https://console.agora.io/)，创建一个项目，进入项目配置页，即可看到APP ID。
         */
        buildConfigField "String", "AGORA_APP_ID", "\"${properties.getProperty("AGORA_APP_ID")}\""
        /**
         * Agora APP Certificate.
         * Agora provides App certificate to generate Token. You can deploy and generate a token on your server,
         * or use the console to generate a temporary token.
         * In order to get the APP ID, you can open the agora console (https://console.agora.io/) to create a project with the App Certificate enabled,
         * then the APP Certificate can be found in the project detail page.
         * PS: If the project does not have certificates enabled, leave this field blank.
         *
         * 声网APP证书
         * Agora 提供 App certificate 用以生成 Token。您可以在您的服务器部署并生成 Token，或者使用控制台生成临时的 Token。
         * 进入声网控制台(https://console.agora.io/)，创建一个带证书鉴权的项目，进入项目配置页，即可看到APP证书。
         * 注意：如果项目没有开启证书鉴权，这个字段留空。
         */
        buildConfigField "String", "AGORA_CERTIFICATE", "\"${properties.getProperty("AGORA_CERTIFICATE")}\""

        buildConfigField "String", "AGORA_APP_KEY", "\"${properties.getProperty("AGORA_APP_KEY")}\""

        buildConfigField "String", "SERVER_HOST", "\"${properties.getProperty("SERVER_HOST")}\""

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        //用于设置使用as打包so时指定输出目录
        externalNativeBuild {
            ndkBuild {
                abiFilters  "arm64-v8a","armeabi-v7a"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('../keystore')
            storePassword '230713'
            keyAlias 'voiceroom'
            keyPassword '230713'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            res.srcDirs += "src/main/java/io/agora/app/voice/kit/res"
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                outputFileName = new File(rootProject.name + "_" +
                        "v" + defaultConfig.versionName + "_" +
                        new Date().format("yyyyMMddHHmm") + "_" +
                        buildType.name + ".apk")
            }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.15.0'
    implementation project(':asceneskit')
}